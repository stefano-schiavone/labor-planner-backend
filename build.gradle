plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'

   id("org.sonarqube") version "7.1.0.6387"
   id 'jacoco'
}

group = 'com.laborplanner'
version = '0.0.1-SNAPSHOT'
description = 'Spring Boot Backend for Labor Planner Application'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

def optaplannerVersion = "9.44.0.Final"

dependencies {
   // Core Spring Boot
   implementation 'org.springframework.boot:spring-boot-starter'

   // Spring Web (for @Controller, REST endpoints, and serving HTML)
   implementation 'org.springframework.boot:spring-boot-starter-web'

   // Thymeleaf (for server-side HTML templates)
   implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

   // Spring Data JPA
   implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

   // Lombok
   compileOnly 'org.projectlombok:lombok:1.18.42'
   annotationProcessor 'org.projectlombok:lombok:1.18.42'

   // MapStruct
   implementation 'org.mapstruct:mapstruct:1.6.3'
   annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

   // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security:3.5.8'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

   // PostgreSQL
   implementation 'org.postgresql:postgresql:42.6.0'

   // Swagger
   implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
   implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'
   implementation 'jakarta.el:jakarta.el-api:6.0.1'

   // Optaplanner
   implementation platform("org.optaplanner:optaplanner-bom:${optaplannerVersion}")
   implementation "org.optaplanner:optaplanner-spring-boot-starter"
   testImplementation("org.optaplanner:optaplanner-test")

   // Serializing Duration
   implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

   // H2 Database
   testImplementation 'com.h2database:h2'

   // Lombok Tests
   testCompileOnly 'org.projectlombok:lombok:1.18.42'
   testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
   implementation 'org.springframework.boot:spring-boot-starter-validation'
   testImplementation 'org.springframework.boot:spring-boot-starter-validation'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(JavaCompile) {
    options.annotationProcessorPath = configurations.annotationProcessor
    options.compilerArgs += ['-parameters']
}


// Customizing Test output

tasks.named('test') {
	useJUnitPlatform()
}

// For loading .env file
bootRun {
    doFirst {
        def dotenvFile = file(".env")
        if (dotenvFile.exists()) {
            dotenvFile.eachLine { line ->
                if (!line.startsWith("#") && line.contains("=")) {
                    def (key, value) = line.split("=", 2)
                    environment key.trim(), value.trim()
                }
            }
        }
    }
}

ext.testSummary = [
    unit: [success: 0, failed: 0, skipped: 0, duration: 0.0],
    integration: [success: 0, failed: 0, skipped: 0, duration: 0.0],
    htmlReport: ''
]

tasks.named('test') { testTask ->
    def startTime = 0L
    doFirst { startTime = System.nanoTime() }

    useJUnitPlatform { excludeTags 'integration' }
    ignoreFailures = true
    testLogging {
        events "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat "full"
        showStandardStreams = true
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            testSummary.unit.success = result.successfulTestCount as int
            testSummary.unit.failed = result.failedTestCount as int
            testSummary.unit.skipped = result.skippedTestCount as int
            testSummary.unit.duration = (System.nanoTime() - startTime) / 1_000_000_000.0
        }
    }

    // Run integrationTest after unit test
    finalizedBy tasks.named('integrationTest')
}

task integrationTest(type: Test) {
    description = 'Runs integration tests'
    group = 'verification'

    def startTime = 0L
    doFirst { startTime = System.nanoTime() }

    useJUnitPlatform { includeTags 'integration' }
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    ignoreFailures = true
    testLogging {
        events "FAILED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"
        exceptionFormat "full"
        showStandardStreams = true
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            testSummary.integration.success = result.successfulTestCount as int
            testSummary.integration.failed = result.failedTestCount as int
            testSummary.integration.skipped = result.skippedTestCount as int
            testSummary.integration.duration = (System.nanoTime() - startTime) / 1_000_000_000.0
        }
    }
}

gradle.buildFinished {
    def totalDuration = testSummary.unit.duration + testSummary.integration.duration
    def totalTests = testSummary.unit.success + testSummary.unit.failed + testSummary.unit.skipped +
                     testSummary.integration.success + testSummary.integration.failed + testSummary.integration.skipped
    if (totalTests > 0) {
        println "\n┌───────────────────────────────┐"
        println "│ Combined Test Summary         │"
        println "├───────────────────────────────┤"
        println "│ Unit Successes       : ${testSummary.unit.success}"
        println "│ Unit Failures        : ${testSummary.unit.failed}"
        println "│ Unit Skipped         : ${testSummary.unit.skipped}"
        println "│ Unit Duration (s)    : ${String.format('%.2f', testSummary.unit.duration)}"
        println "│ Integration Success  : ${testSummary.integration.success}"
        println "│ Integration Failures : ${testSummary.integration.failed}"
        println "│ Integration Skipped  : ${testSummary.integration.skipped}"
        println "│ Integration Duration(s): ${String.format('%.2f', testSummary.integration.duration)}"
        println "│ Total Duration (s)   : ${String.format('%.2f', totalDuration)}"
        println "└───────────────────────────────┘"
    } else {
        println "\nNo tests were executed during this Gradle invocation - skipping combined test summary."
    }
}

// For SonarQube tests
jacoco {
    toolVersion = "0.8.12"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

sonarqube {
    properties {
        // set a sensible sonar.projectKey and name for your project
        property "sonar.projectKey", "I558150_labor-planner-backend_5c2246fe-6e45-4577-ae07-a133c852e12c"
        property "sonar.projectName", "Labor Planner Backend"
        property "sonar.qualitygate.wait", true

        property "sonar.junit.reportPaths", "build/test-results/test"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"

        // Java binaries location for Sonar
        property "sonar.java.binaries", "build/classes/java/main"
    }
}
